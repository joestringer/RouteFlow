dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(nox, 0.4.0~APPS_ID~beta, contact@noxrepo.org)
AC_CONFIG_AUX_DIR(config)

NX_BUILDNR
AH_BOTTOM([/* NOX version with build number, e.g. "1.2.3+build456". */
#define NOX_VERSION VERSION BUILDNR])

AM_INIT_AUTOMAKE([tar-ustar])

AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP

AC_USE_SYSTEM_EXTENSIONS

AC_DISABLE_STATIC
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL

CHECK_OPENFLOW

AC_CHECK_FUNCS([fdatasync ppoll])
AC_CONFIG_SRCDIR([src/])
AC_CONFIG_HEADER([config.h])

AX_BOOST_BASE([1.34.1])

AX_BOOST_FILESYSTEM
if test "$ax_cv_boost_filesystem" != "yes"; then
   AC_MSG_ERROR([cannot find the Boost::Filesystem library])
fi

AX_LIB_XERCES([2.7.0])
if test "x$HAVE_XERCES" != "xyes"; then
   AC_MSG_ERROR([cannot find the Apache Xerces library])
fi

CHECK_PCAP
CHECK_SSL
CHECK_NDEBUG
CHECK_PROFILING
CHECK_COVERAGE
CHECK_NETLINK
CHECK_THREAD_LOCAL
CHECK_WITH_VM
CHECK_LTDL
CHECK_LOG4CXX

dnl Add your module here to have it grouped into a package

ACI_PACKAGE([coreapps],[core application set],
               [pyrt simple_c_app simple_c_py_app hub switch examples
	       messenger openflowpacket spanning_tree mpls],
               [yes])
ACI_PACKAGE([netapps],[misc network apps],
               [configuration authenticator
               routing user_event_log storage tests topology discovery
               directory bindings_storage switchstats flow_fetcher
               lavi routeinstaller dhcp flowdb sqlitelog
	           rfproxy],
               [TURN_ON_NETAPPS])
ACI_PACKAGE([uiapps],[misc ui apps],
               [storagews miscws default_switch_approval switchconfig],
               [TURN_ON_UIAPPS])

AC_SUBST(ACTIVE_PACKAGES)
AC_SUBST(MODULES_coreapps)               
AC_SUBST(MODULES_netapps)               
AC_SUBST(MODULES_uiapps)

AM_CONDITIONAL([HAVE_NETAPPS], [test -n "$MODULES_netapps"])

dnl
dnl Conditional checks based on what modules have been turned on.
dnl

if test "$HAVE_coreapps" = "yes"; then
    CHECK_TWISTED
    dnl only check for swig if python was enabled
    if test -n "$PYTHON"; then
        AC_PROG_SWIG(1.3.0)
        if test -z "$SWIG"; then
            AC_MSG_ERROR([When configured with python, NOX requires swig 1.3.0 or later to be installed])
        fi
        SWIG_ENABLE_CXX
        SWIG_PYTHON
    fi    
fi

if test "$HAVE_netapps" = "yes"; then
    CHECK_SQLITE3
fi

if test "$HAVE_netapps" = "yes"; then
    AX_BOOST_SERIALIZATION
    if test "$ax_cv_boost_serialization" != "yes"; then
       AC_MSG_ERROR([cannot find the Boost::Serialization library])
    fi
fi

if test "$HAVE_netapps" = "yes"; then
    AX_BOOST_UNIT_TEST_FRAMEWORK
    if test "$ax_cv_boost_unit_test_framework" != "yes"; then
       AC_MSG_ERROR([cannot find the Boost::Unit_Test_Framework library])
    fi
fi


AC_ARG_ENABLE(
  [noext],
  [AC_HELP_STRING([--enable-noext],
                  [Ignore ext directory even if it exists])],
  [case "${enableval}" in # (
     yes) noext=true ;; # (
     no)  noext=false ;; # (
     *) AC_MSG_ERROR([bad value ${enableval} for --enable-noext]) ;;
   esac],
  [noext=false]
)

if test -d "$srcdir/src/nox/ext/"; then
    if test "$noext" = false; then
        AC_CONFIG_SUBDIRS([src/nox/ext])
    fi
fi

if test "$log4cxx" = true; then
    AC_CONFIG_SUBDIRS([src/nox/thirdparty/apache-log4cxx])
fi

# AC_CONFIG FILES GENERATED BY ./boot.sh Don't put anything below this
# line!
